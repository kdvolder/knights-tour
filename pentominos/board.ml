open Knights_tour

module PointMap = Map.Make(Point)

type square =
  | Occupied of Polyomino.t 
  | Vacant
  | Blocked

type t = {
  size: Point.t;
  squares: Polyomino.t option PointMap.t; (* map of squares on the board excluding blocked squares *)
}

let size {size;_} = size

let get {squares;_} pt =
  match PointMap.find_opt pt squares with 
  | None -> Blocked
  | Some (Some p) -> Occupied p
  | Some None -> Vacant

let vacant {squares;_} =
  let vacancies = ref PointSet.empty in
  squares |> PointMap.iter (fun pt square ->
    if Option.is_none square then
      vacancies := PointSet.add pt !vacancies
  );
  !vacancies
  
let of_string img =
  let vacancies = PointSet.of_string img |> PointSet.normalize_translation in
  {
    size = Point.{
      x = PointSet.max_x vacancies + 1;
      y = PointSet.max_y vacancies + 1;
    };
    squares = PointSet.fold (fun vacant board -> PointMap.add vacant None board) vacancies PointMap.empty
  }

let classic_board = of_string "
  ########
  ########
  ########
  ###..###
  ###..###
  ########
  ########
  ########"

let%expect_test "Board of_string |> vacant" =
  let open Printf in
  let board = classic_board in
  printf "min x: %d y: %d\n" (PointSet.min_x (vacant board)) (PointSet.min_y (vacant board));
  printf "max x: %d y: %d\n" (PointSet.max_x (vacant board)) (PointSet.max_y (vacant board));
  printf "size = (%d, %d)\n" (size board).x (size board).y;
  printf "\n";
  printf "%s" (PointSet.to_string (vacant board));
  [%expect{|
    min x: 0 y: 0
    max x: 7 y: 7
    size = (8, 8)

    ########
    ########
    ########
    ###..###
    ###..###
    ########
    ########
    ######## |}]

let put board points poly =
  PointSet.fold (fun pt board ->
    match PointMap.find pt board with 
    | None -> PointMap.add pt (Some poly) board
    | _ -> failwith "Invalid placement. Polyomino should only be placed on vacant squares"
  ) points board.squares
  |> (fun squares -> {board with squares})  

module PolyMap = Map.Make(Polyomino)

let poly_namer () =
  let nextChar = ref 'A' in
  let polyNames = ref PolyMap.empty in 

  let getName poly = 
    match PolyMap.find_opt poly !polyNames with
    | Some existingName -> existingName
    | None -> let newName = !nextChar in
        nextChar := (Char.code newName) + 1 |> Char.chr;
        polyNames := PolyMap.add poly newName !polyNames;
        newName
  in getName

let to_string poly_namer board =
  let square_to_char = (function
  | Vacant -> '.'
  | Occupied p -> poly_namer p
  | Blocked -> '#'
  ) in

  let size = board.size in
  let image = Buffer.create ((size.x+1)*(size.y+1)) in
  for y = 0 to size.y-1 do
    for x = 0 to size.x-1 do
      Buffer.add_char image (square_to_char (get board {x;y}))
    done;
    Buffer.add_char image '\n'
  done;
  Buffer.contents image

let%expect_test "Place a polyomino" =
  let polyos = Polyomino.of_order 5 in
  let board = classic_board in
  polyos |> Searchspace.to_seq |> Seq.iteri (fun i poly ->
    Printf.printf "%d:\n" (i+1);
    let board = put board (Polyomino.points poly) poly in
    Printf.printf "%s\n" (to_string (poly_namer ()) board)
  )
  ; [%expect{|
    1:
    AA......
    .AA.....
    .A......
    ...##...
    ...##...
    ........
    ........
    ........

    2:
    .A......
    AAA.....
    .A......
    ...##...
    ...##...
    ........
    ........
    ........

    3:
    AAAA....
    .A......
    ........
    ...##...
    ...##...
    ........
    ........
    ........

    4:
    AAA.....
    AA......
    ........
    ...##...
    ...##...
    ........
    ........
    ........

    5:
    AAA.....
    .A......
    .A......
    ...##...
    ...##...
    ........
    ........
    ........

    6:
    AA......
    .AA.....
    ..A.....
    ...##...
    ...##...
    ........
    ........
    ........

    7:
    AAA.....
    ..AA....
    ........
    ...##...
    ...##...
    ........
    ........
    ........

    8:
    AA......
    .A......
    .AA.....
    ...##...
    ...##...
    ........
    ........
    ........

    9:
    AAAA....
    A.......
    ........
    ...##...
    ...##...
    ........
    ........
    ........

    10:
    AAA.....
    A.A.....
    ........
    ...##...
    ...##...
    ........
    ........
    ........

    11:
    AAA.....
    A.......
    A.......
    ...##...
    ...##...
    ........
    ........
    ........

    12:
    AAAAA...
    ........
    ........
    ...##...
    ...##...
    ........
    ........
    ........ |}]
